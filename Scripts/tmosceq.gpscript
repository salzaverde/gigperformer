Include "tmoscchanneltype"

// Variables
var bandTypes: String Array = ["Bell", "Shelf", "HP", "LP"]
var currentBandType: String Array = []

const numBands: Integer = 3
const numTypeSelectableBands: Integer = 2
var faderIndices: Integer Array = []

// State
var eqsEnabled: Boolean Array = []
var hpsEnabled: Boolean Array = []

var eqUpdatesPending: Integer = 0
var hpUpdatesPending: Integer = 0
var updateEqIndex: Integer = -1

//Functions
Function toBandTypeValue(bandIndex: Integer, bandType: String) Returns Double
    Select
        bandType == "Bell" Do
            result = 0.0
        bandType == "Shelf" Do
            result = 0.333
        bandType == "HP" Do
            If bandIndex == 0 Then
                result = 0.666
            Else
                result = 1.0
            End
        bandType == "LP" Do
            If bandIndex == 0 Then
                result = 1.0
            Else
                result = 0.666
            End
    End
End

Function updateToggleStates ()
    setChannelType(InputChannelType)
    
    eqUpdatesPending = Size(faderIndices)
    hpUpdatesPending = Size(faderIndices)
    updateEqIndex = 0

    OSC_SendDouble("/setOffsetInBank", faderIndices[0])
End

// API
Function setEQEnabled(eqIndex: Integer, enabled: Boolean)
    var faderIndex: Integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        If eqsEnabled[eqIndex] != enabled Then
            faderIndex = faderIndices[eqIndex]
            
            setChannelType(InputChannelType)
            OSC_SendDouble("/setOffsetInBank", faderIndex)
            OSC_SendDouble("/2/eqEnable", 1.0)

            eqsEnabled[eqIndex] = enabled
        End
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setHPEnabled(eqIndex: Integer, enabled: Boolean)
    var faderIndex: Integer
    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        If hpsEnabled[eqIndex] != enabled Then
            faderIndex = faderIndices[eqIndex]
            
            setChannelType(InputChannelType)
            OSC_SendDouble("/setOffsetInBank", faderIndex)
            OSC_SendDouble("/2/lowcutEnable", 1.0)

            hpsEnabled[eqIndex] = enabled
        End
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setHPDBPerOctave(eqIndex: Integer, dbPerOctave: Integer)
    var faderIndex: Integer
    var value: Double

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        Select
            dbPerOctave == 6 Do
                value = 0.0
            dbPerOctave == 12 Do
                value = 0.333
            dbPerOctave == 18 Do
                value = 0.666
            dbPerOctave == 24 Do
                value = 1.0
        End

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/lowcutGrade", value)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setHPFrequency(eqIndex: Integer, frequency: Double)
    var faderIndex: integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/lowcutFreq", frequency)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setFrequency(eqIndex: Integer, bandIndex: Integer, frequency: Double)
    var faderIndex: integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqFreq" + (bandIndex + 1), frequency)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setGain(eqIndex: Integer, bandIndex: Integer, gain: Double)
    var faderIndex: integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqGain" + (bandIndex + 1), gain)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setQ(eqIndex: Integer, bandIndex: Integer, gain: Double)
    var faderIndex: Integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqQ" + (bandIndex + 1), gain)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function getBandType(eqIndex: Integer, bandIndex: Integer) Returns String
    var bandTypeIndex: Integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        bandTypeIndex = toMapIndex(eqIndex, bandIndex, numBands)
        result = currentBandType[bandTypeIndex]
    Else
        Print("EQ index out of bounds: " + eqIndex)
        result = "Unknown"
    End
End

Function setBandType(eqIndex: Integer, bandIndex: Integer, bandType: String)
    var faderIndex: Integer
    var bandTypeIndex: Integer
    var bandTypeValue: Double

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        bandTypeIndex = toMapIndex(eqIndex, bandIndex, numBands)
        currentBandType[bandTypeIndex] = bandType

        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqType" + (bandIndex + 1), toBandTypeValue(bandIndex, bandType))
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function nextBandType(eqIndex: Integer, bandIndex: Integer) Returns String
    var bandTypeIndex: Integer
    var currentType: String
    var nextIndex: Integer

    result = "Unknown"

    currentType = getBandType(eqIndex, bandIndex)
    For bandTypeIndex = 0; bandTypeIndex < Size(bandTypes); bandTypeIndex = bandTypeIndex + 1 Do
        If bandTypes[bandTypeIndex] == currentType Then
            nextIndex = (bandTypeIndex + 1) % Size(bandTypes)
            setBandType(eqIndex, bandIndex, bandTypes[nextIndex])
            result = bandTypes[nextIndex]
        End
    End
End

Function addEQ(faderIndex: Integer, eqEnabled: Boolean, hpEnabled: Boolean)
    faderIndices <-- faderIndex
    eqsEnabled <-- eqEnabled
    hpsEnabled <-- hpEnabled
    
    // Middle band does not support band type
    currentBandType <-- "Bell"
    currentBandType <-- ""
    currentBandType <-- "Bell"
End

Function processNextUpdate()
    If eqUpdatesPending > 0 And eqUpdatesPending == hpUpdatesPending Then
        updateEqIndex = Size(faderIndices) - eqUpdatesPending

        OSC_SendDouble("/setOffsetInBank", faderIndices[updateEqIndex])
    End
End

Function initialize()
    OSC_SendDouble("/setBankStart", 0.0)
    updateToggleStates()
End

On OSCMessageReceived(message : OSCMessage) Matching "/2/eqEnable"
    var enabled: Boolean
    
    If eqUpdatesPending > 0 Then
        enabled = OSC_GetArgAsDouble(message, 0) > 0.5
        If eqsEnabled[updateEqIndex] != enabled Then
            OSC_SendDouble("/2/eqEnable", 1.0)
        End
        eqUpdatesPending = eqUpdatesPending - 1
        processNextUpdate()
    End
End

On OSCMessageReceived(message : OSCMessage) Matching "/2/lowcutEnable"
    var enabled: Boolean
    
    If eqUpdatesPending > 0 Then
        enabled = OSC_GetArgAsDouble(message, 0) > 0.5
        If hpsEnabled[updateEqIndex] != enabled Then
            OSC_SendDouble("/2/lowcutEnable", 1.0)
        End
        hpUpdatesPending = hpUpdatesPending - 1

        processNextUpdate()
    End
End