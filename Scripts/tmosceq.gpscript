Include "tmoscchanneltype"

// Variables
var bandTypes: String Array = ["Bell", "Shelf", "HP", "LP"]
var currentBandType: String Array = []

const numBands: Integer = 3
const numTypeSelectableBands: Integer = 2
var faderIndices: Integer Array = []

// State
var eqIndicesPending: Integer Array = []
var eqsEnabled: Boolean Array = []

//Functions
Function toBandTypeValue(bandIndex: Integer, bandType: String) Returns Double
    Select
        bandType == "Bell" Do
            result = 0.0
        bandType == "Shelf" Do
            result = 0.333
        bandType == "HP" Do
            If bandIndex == 0 Then
                result = 0.666
            Else
                result = 1.0
            End
        bandType == "LP" Do
            If bandIndex == 0 Then
                result = 1.0
            Else
                result = 0.666
            End
    End
End

Function updateEqEnabledState (eqIndex: Integer)
    var faderIndex: Integer

    setChannelType(InputChannelType)
            
    eqIndicesPending <-- eqIndex
    faderIndex = faderIndices[eqIndex]

    // eqEnable is a toggle, actual value might be set in callback
    OSC_SendDouble("/setOffsetInBank", faderIndex)
    OSC_SendDouble("/2/eqEnable", 1.0)
End

// API
Function setEQEnabled(eqIndex: Integer, enabled: Boolean)
    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        If eqsEnabled[eqIndex] != enabled Then
            eqsEnabled[eqIndex] = enabled
            updateEqEnabledState(eqIndex)
        End
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setFrequency(eqIndex: Integer, bandIndex: Integer, frequency: Double)
    var faderIndex: integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqFreq" + (bandIndex + 1), frequency)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setGain(eqIndex: Integer, bandIndex: Integer, gain: Double)
    var faderIndex: integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqGain" + (bandIndex + 1), gain)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function setQ(eqIndex: Integer, bandIndex: Integer, gain: Double)
    var faderIndex: Integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqQ" + (bandIndex + 1), gain)
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function getBandType(eqIndex: Integer, bandIndex: Integer) Returns String
    var bandTypeIndex: Integer

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        bandTypeIndex = toMapIndex(eqIndex, bandIndex, numBands)
        result = currentBandType[bandTypeIndex]
    Else
        Print("EQ index out of bounds: " + eqIndex)
        result = "Unknown"
    End
End

Function setBandType(eqIndex: Integer, bandIndex: Integer, bandType: String)
    var faderIndex: Integer
    var bandTypeIndex: Integer
    var bandTypeValue: Double

    If eqIndex >= 0 And eqIndex < Size(faderIndices) Then
        faderIndex = faderIndices[eqIndex]
        bandTypeIndex = toMapIndex(eqIndex, bandIndex, numBands)
        currentBandType[bandTypeIndex] = bandType

        setChannelType(InputChannelType)

        OSC_SendDouble("/setOffsetInBank", faderIndex)
        OSC_SendDouble("/2/eqType" + (bandIndex + 1), toBandTypeValue(bandIndex, bandType))
    Else
        Print("EQ index out of bounds: " + eqIndex)
    End
End

Function nextBandType(eqIndex: Integer, bandIndex: Integer) Returns String
    var bandTypeIndex: Integer
    var currentType: String
    var nextIndex: Integer

    result = "Unknown"

    currentType = getBandType(eqIndex, bandIndex)
    For bandTypeIndex = 0; bandTypeIndex < Size(bandTypes); bandTypeIndex = bandTypeIndex + 1 Do
        If bandTypes[bandTypeIndex] == currentType Then
            nextIndex = (bandTypeIndex + 1) % Size(bandTypes)
            setBandType(eqIndex, bandIndex, bandTypes[nextIndex])
            result = bandTypes[nextIndex]
        End
    End
End

Function addEQ(faderIndex: Integer)
    faderIndices <-- faderIndex
    eqsEnabled <-- false
    
    // Middle band does not support band type
    currentBandType <-- "Bell"
    currentBandType <-- ""
    currentBandType <-- "Bell"

    updateEqEnabledState(Size(faderIndices) - 1)
End

// Initialization
Initialization
    OSC_SendDouble("/setBankStart", 0.0)
End

On OSCMessageReceived(message : OSCMessage) Matching "/2/eqEnable"
    var enabled: Boolean
    var eqIndexPending: Integer
    var i: Integer
    var remainingIndices: Integer Array

    If Size(eqIndicesPending) > 0 Then
        eqIndexPending = eqIndicesPending[0]
        enabled = OSC_GetArgAsDouble(message, 0) > 0.5
        If enabled != eqsEnabled[eqIndexPending] Then
            OSC_SendDouble("/2/eqEnable", 1.0)
        End
    End

    For i = 1; i < Size(eqIndicesPending) - 1; i = i + 1 Do
        remainingIndices <-- eqIndicesPending[i]
    End

    eqIndicesPending = remainingIndices
End