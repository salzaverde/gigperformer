Include "routing"
Include "tmosc"

// Configuration

// PlaybackChannels are used for virtual instruments
// InputChannels are used for audio inputs
// Check inputChannels in routing.gpscript for channel names
var tmChannelTypes: Integer Array = [InputChannelType, InputChannelType, PlaybackChannelType, InputChannelType, InputChannelType]

// Which TotalMix channel index the channels should be mapped to (indices can repeat if they are of different types)
var tmInputChannelIndices: Integer Array = [5, 4, 1, 0, 1]

// Check outputChannels in routing.gpscript for channel names
// Outputchannels are set using setSubmix, where the value corresponds to single channels (e.g. first channel of stereo pair)
var tmOutputChannelIndices: Integer Array = [1, 3, 5, 7, 9]
var tmSummingChannelIndices: Integer Array = [1, 1, 1, 7, 9]

// Widgets
var sumBussesToStereoSwitch: Widget

// Variables
var tmRoutes: Boolean Array = [] // How input channels route to output channels


// Functions
Function resetTMRoutes()
    var i, j, inputChannelIndex, outputChannelIndex: Integer
    For i = 0; i < Size(tmOutputChannelIndices); i = i + 1 Do
        For j = 0; j < Size(tmInputChannelIndices); j = j + 1 Do
            outputChannelIndex = tmOutputChannelIndices[i]
            inputChannelIndex = tmInputChannelIndices[j]
            setChannelLevel(outputChannelIndex, inputChannelIndex, tmChannelTypes[j], 0.0)
        End
    End
End

Function buildTMRoutes()
    var level: Double
    var outputChannel: Integer
    var inputChannel: Integer
    var routeIndex: Integer
    var tmOutputIndex: Integer
    var tmInputIndex: Integer

    var sumToStereo: Boolean = GetWidgetValue(sumBussesToStereoSwitch) > 0.5

    resetTMRoutes()

    For routeIndex = 0; routeIndex < Size(channelRoutes); routeIndex = routeIndex + 1 Do
        outputChannel = routeIndex / Size(inputChannels)
        inputChannel = routeIndex % Size(inputChannels)

        If sumToStereo Then
            tmOutputIndex = tmSummingChannelIndices[outputChannel]
        Else
            tmOutputIndex = tmOutputChannelIndices[outputChannel]
        End
        
        tmInputIndex = tmInputChannelIndices[inputChannel]

        If channelRoutes[routeIndex] Then
            level = 0.80742913 // 0dB
            setChannelLevel(tmOutputIndex, tmInputIndex, tmChannelTypes[inputChannel], level)
        End
    End
End

Function updateRouting () 
    updateBusRoutes()
    updateBusInserts()
    buildChannelRoutes()
    buildTMRoutes()
End

// Initialize
Initialization
    var i, j: Integer
    For i = 0; i < Size(tmOutputChannelIndices); i = i + 1 Do
        For j = 0; j < Size(tmInputChannelIndices); j = j + 1 Do
            tmRoutes <-- false
        End
    End
    
    updateRouting()
End


// Callbacks
// This needs to be triggered from rackspace but rest should be global
On WidgetValueChanged (w : Widget, index : integer, value : double) from audio00, audio01, audio02, audio10, audio11, audio12, audio20, audio21, audio22
    updateRouting()
End

On WidgetValueChanged (w : Widget, index : integer, value : double) from insert00, insert01, insert02, insert10, insert11, insert12
    updateRouting()
End

On WidgetValueChanged (value : double) from sumBussesToStereoSwitch
    updateRouting()
End
